" My personal vimrc file.
"
" Maintainer:	Andrea Riciputi <andrea.riciputi@gmail.com>
"

" I like fish, but vim doesn't
if &shell =~# 'fish$'
    set shell=sh
endif

set nocompatible        " Vim is more than Vi

call plug#begin('~/.vim/plugged')

Plug 'junegunn/vim-plug'                            " Required to have plug's docs available

" ----------------------------------------
" Plugins I use to make Vim a better place
" ----------------------------------------

Plug 'ervandew/supertab'                            " SuperTab
Plug 'henrik/vim-indexed-search'                    " Display #/N matches when searching
Plug 'jlanzarotta/bufexplorer'                      " BufferExplorer
Plug 'plytophogy/vim-diffchanges'                   " Shows changes since the last save
Plug 'luochen1990/rainbow'                          " Rainbow parenthesis
Plug 'scrooloose/nerdcommenter'                     " Make comment/uncomment easy
Plug 'tpope/vim-capslock'                           " Capslock without capslock key
Plug 'tpope/vim-surround'                           " Make changing surround chars easier
Plug 'maxbrunsfeld/vim-yankstack'                   " Kill-ring please!
Plug 'nathanaelkane/vim-indent-guides'              " Mark indentation level
Plug 'christoomey/vim-system-copy'                  " Integrate vim with system clipboard
Plug 'nanotech/jellybeans.vim'                      " Colorscheme (at last!)

" --------------------------
" File-type specific plugins
"
" If you ever need other file-type specific plugins have a look at:
" https://github.com/sheerun/vim-polyglot
" --------------------------

Plug 'chrisbra/csv.vim'                             " CSV plugin
Plug 'dag/vim-fish'                                 " Support for fish-shell scripts
Plug 'pangloss/vim-javascript'                      " Better Javascript syntax and indent
Plug 'lifepillar/pgsql.vim'                         " PostgreSQL syntax and indent
Plug 'tmhedberg/SimpylFold'                         " Better folding for Python

"Plug 'vim-python/python-syntax'                     " Better Python syntax highlighting
Plug '~/Documents/Development/python-syntax'        " Better Python syntax highlighting
Plug 'vim-scripts/indentpython.vim'                 " Better Python indentation
Plug 'jeetsukumaran/vim-pythonsense'                " Better Python motion

" ----------------------
" External tools plugins
" ----------------------

Plug 'tpope/vim-fugitive'                           " Git integration
Plug 'mhinz/vim-grepper'                            " Grep-like tool integration
Plug 'rizzatti/dash.vim'                            " Dash - Vim integration
Plug 'ludovicchabant/vim-gutentags'                 " Manage tags (via ctags)
Plug 'w0rp/ale'                                     " Syntax checking and linting
Plug 'tpope/vim-dispatch'                           " Async task for vim
Plug 'janko-m/vim-test'                             " Run tests from inside vim

" All of your Plugins must be added before the following line
call plug#end()            " required

"
" Color scheme settings
"

augroup ColorSchemeCustomization
      au!
      autocmd ColorScheme jellybeans hi CursorLine cterm=underline ctermbg='NONE'
      autocmd ColorScheme jellybeans hi Folded ctermfg=241 ctermbg='NONE'
      autocmd ColorScheme jellybeans hi clear Search
      autocmd ColorScheme jellybeans hi clear IncSearch
      autocmd ColorScheme jellybeans hi Search term=reverse cterm=reverse gui=reverse
      autocmd ColorScheme jellybeans hi IncSearch term=reverse cterm=reverse gui=reverse
      autocmd ColorScheme jellybeans hi SignColumn ctermbg='NONE'
      autocmd ColorScheme jellybeans hi! link Todo PreProc
      autocmd ColorScheme jellybeans hi! link pythonStrFormatting Constant
      autocmd ColorScheme jellybeans hi! link pythonClassVar PreCondit
      autocmd ColorScheme jellybeans hi ALEErrorSign ctermfg=1 ctermbg='NONE'
      autocmd ColorScheme jellybeans hi ALEWarningSign ctermfg=3 ctermbg='NONE'
      autocmd ColorScheme jellybeans hi ALEInfoSign ctermfg=2 ctermbg='NONE'
      autocmd ColorScheme jellybeans hi ErrorMsg ctermfg=88 ctermbg='NONE'
      autocmd ColorScheme jellybeans hi DiffAdd ctermfg=70 ctermbg=235
      autocmd ColorScheme jellybeans hi DiffDelete ctermfg=124 ctermbg=235
      autocmd ColorScheme jellybeans hi DiffChange ctermfg=3 ctermbg=235
      autocmd ColorScheme jellybeans hi DiffText cterm=underline ctermfg=3 ctermbg=235
      autocmd ColorScheme jellybeans hi StatusLine ctermfg=16 ctermbg=141
      autocmd ColorScheme jellybeans hi StatusLineNC ctermfg=231 ctermbg=244
augroup END

let g:jellybeans_overrides = {
            \ 'background': { 'ctermbg': 'none', '256ctermbg': 'none' },
            \}

colorscheme jellybeans

"
" Platform recognition settings
"

let s:uname=''
if has('unix')
    let s:uname = system('uname | tr -d "\n"') " Get platform name (stripping the trailing newline)
endif

"
" Common option settings
"

set backspace=indent,eol,start       " allow backspacing over everything in insert mode

set hidden              " hide buffers instead of closing
set nobackup		" do not keep a backup file
set history=1000	" keep 1000 lines of command line history
set ruler		" show the cursor position all the time
set cursorline          " show the cursor line
set showcmd		" display incomplete commands
set tildeop             " set tilde as operator
set encoding=utf-8      " set character enconding to utf-8
set scrolloff=2         " keep always 2 lines of context when scrolling
set textwidth=0         " disable automatic line wrap
set viminfo='100,<50,:100,s10,h " things to remember (see :he viminfo)
set formatoptions+=j    " remove comment chars when joining two comment lines

" Set status bar options
set laststatus=2        " show the status line always

" Set up tab stop machinery
"set tabstop=8           " this is the default, i set it anyway just for documentation
set shiftwidth=4        " set indetation to 4 characters
set expandtab           " expand <TAB> char to spaces

set autoindent          " set autoindent on
set copyindent          " copy the previous indentation on autoindenting

" Set searching options
set ignorecase          " foo matches foo, Foo and FOO
set smartcase           " Foo matches Foo only
set incsearch           " highlight search pattern while typing
set hlsearch            " when done keep highlighting

" Syntax highlighting settings
if &t_Co > 2 || has("gui_running")
  syntax on
endif

" Toggle search highlighting
nnoremap <silent> <Leader>hh :set hlsearch! hlsearch?<CR>
" Highlight current word
nnoremap <Leader>hw :let @/='\<<C-R>=expand("<cword>")<CR>\>'<CR>:set hls<CR>

" Folding settings
set foldlevelstart=1    " Folding root level open

" Invisible characters settings
set listchars=tab:>-,eol:Â¬,trail:~
nnoremap <silent> <Leader>hi :set list! list?<CR>

" Set complete option
set complete-=i " DO NOT scan included files when searching for completion
set completeopt=longest " the colors of the menu make it hard to read and almost useless

" SuperTab options
let g:SuperTabLongestEnhanced=1 " (default value: 0)

" Wildmenu settings
set wildmenu
set wildmode=longest,full
set wildignorecase


" Improve SimpylFold with an ad-hoc PythonFoldText() function
" The PythonFoldText is ashamely stolen from https://github.com/mbertheau/jpythonfold.vim
function! PythonFoldText()
  let fs = v:foldstart
  while getline(fs) =~ '^\s*@' | let fs = nextnonblank(fs + 1)
  endwhile
  let line = getline(fs)
  let nnum = nextnonblank(fs + 1)
  let nextline = getline(nnum)
  "get the document string: next line is ''' or """
  if nextline =~ "^\\s\\+[\"']\\{3}\\s*$"
      let line = line . " " . matchstr(getline(nextnonblank(nnum + 1)), '^\s*\zs.*\ze$')
  "next line starts with qoutes, and has text
  elseif nextline =~ "^\\s\\+[\"']\\{1,3}"
      let line = line." ".matchstr(nextline, "^\\s\\+[\"']\\{1,3}\\zs.\\{-}\\ze['\"]\\{0,3}$")
  elseif nextline =~ '^\s\+pass\s*$'
    let line = line . ' pass'
  endif
  "compute the width of the visible part of the window (see Note above)
  let w = winwidth(0) - &foldcolumn - (&number ? 8 : 0) - (&signcolumn == 'no' ? 0 : 2)
  let size = 1 + v:foldend - v:foldstart
  "compute expansion string
  let spcs = '................'
  while strlen(spcs) < w | let spcs = spcs . spcs
  endwhile
  "expand tabs (mail me if you have tabstop>10)
  let onetab = strpart('          ', 0, &tabstop)
  let line = substitute(line, '\t', onetab, 'g')
  return strpart(line.spcs, 0, w-strlen(size)-7).'.'.size.' lines'
endfunction

"
" Settings that require autocommands
"
if has("autocmd")
    " Put these in an autocmd group, so that we can delete them easily.
    augroup CursorLine   " Toggle cursorline on/off
        au!
        autocmd WinEnter,InsertLeave * set cursorline
        autocmd WinLeave,InsertEnter * set nocursorline
    augroup END

    augroup SyntaxHighlighting   " Syntax highlighting settings
        au!
        if &t_Co > 2 || has("gui_running")
          au BufEnter * syn sync fromstart
        endif
    augroup END

    augroup FileTypeSettings " File type settings
        " shiftwidth == sw, softtabstop == sts, expandtab == et
        " formatoptions == fo, iskeyword == isk, textwidth == tw
        au!
        autocmd FileType python setlocal tw=80 sw=4 sts=4 et fo-=t cc=99 foldtext=PythonFoldText()
        autocmd FileType javascript setlocal tw=80 ts=4 sw=4 sts=4 et fo-=t isk+="-,"
        autocmd FileType css setlocal tw=80 sw=4 sts=4 et fo-=t
        autocmd FileType html setlocal tw=80 sw=2 sts=2 et fo-=t
        autocmd FileType sql setlocal tw=80 et fo-=t
        autocmd FileType rst setlocal tw=70 sw=2 sts=2 et fo+=t
        autocmd FileType tcl setlocal tw=80 sw=4 sts=4 et fo-=t
        autocmd FileType markdown setlocal tw=80 sw=2 sts=2 et fo+=t
    augroup END

    augroup OnQuit
        au!
        " Close Vim if the last window is quickfix
        autocmd BufEnter *
                    \ if &buftype=="quickfix" && winbufnr(2) == -1 |
                    \   quit! |
                    \ endif
    augroup END

    " Remove any trailing whitespace (except when the file is binary or when doing a merge)
    augroup TrailingWhiteSpaces
        au!
        autocmd BufWritePre,FileWritePre * if !&bin && !&diff | %s/\s\+$//e | endif
    augroup END

endif " has("autocmd")

"
" Settings enabled only when vim is started from with pgsql
"
if match(getcwd(), "/pgsql") >=0 ||  match(getcwd(), "/postgresql") >= 0

  set cinoptions=(0
  set tabstop=4
  set shiftwidth=4
  set noexpandtab

endif

"
" Mappings and convinience commands/functions
"

" Space is a better leader
map <Space> <Leader>

" Remap CTRL-I and CTRL-O
" (since CTRL-I is <Tab> and the latter is taken by SuperTab plugin)
nnoremap <Leader>i <C-I>
nnoremap <Leader>o <C-O>

" Make mapping timeout a little bit longer
set timeout timeoutlen=1100

" Don't use Ex mode, use Q for formatting
noremap Q gq

" Avoid to completely lose text when typing C-u or C-w while in insert mode
inoremap <C-u> <C-g>u<C-u>
inoremap <C-w> <C-g>u<C-w>

" Toggle relative line numbers
nnoremap <silent> <Leader>rr :set relativenumber! relativenumber?<CR>

" Re-select the text block just pasted
nnoremap <Leader>vv V`]

" Quickly open the ~/.vimrc file
nnoremap <Leader>ev <C-w><C-v><C-l>:e $MYVIMRC<CR>

" Set paste/nopaste mapping
nnoremap <C-p><C-p> :set invpaste paste?<CR>
set pastetoggle=<C-p><C-p>

" Create Blank Newlines and stay in Normal mode
nnoremap <silent> <Leader>la :set paste<CR>m`o<Esc>``:set nopaste<CR>
nnoremap <silent> <Leader>lA :set paste<CR>m`O<Esc>``:set nopaste<CR>

" Map window management shortcut
nnoremap <silent> <Leader>wh <C-w>h
nnoremap <silent> <Leader>wj <C-w>j
nnoremap <silent> <Leader>wk <C-w>k
nnoremap <silent> <Leader>wl <C-w>l
nnoremap <silent> <Leader>ww <C-w>w

nnoremap <silent> <Leader>w= <C-w>=
nnoremap <silent> <Leader>w_ <C-w>_
nnoremap <silent> <Leader>w- <C-w>-
nnoremap <silent> <Leader>w+ <C-w>+
nnoremap <silent> <Leader>w< <C-w><
nnoremap <silent> <Leader>w> <C-w>>
nnoremap <silent> <Leader>ws <C-w>s
nnoremap <silent> <Leader>wv <C-w>v
nnoremap <silent> <Leader>wo <C-w>o
nnoremap <silent> <Leader>wc <C-w>c

nnoremap <silent> <TAB> <C-w>w
nnoremap <silent> [Z <C-w>W

" Close localfix and quickfix buffers
nnoremap <silent> <Leader>lc :lclose<CR>
nnoremap <silent> <Leader>lo :lopen<CR>
nnoremap <silent> <Leader>qc :cclose<CR>
nnoremap <silent> <Leader>qo :copen<CR>

" Close buffer
nnoremap <silent> <Leader>bc :bdelete<CR>
nnoremap <silent> <Leader>bn :bnext<CR>
nnoremap <silent> <Leader>bN :bNext<CR>
nnoremap <silent> <Leader>bp :bprevious<CR>

" Readline-like movements on the command line
cnoremap <C-a> <Home>
cnoremap <C-d> <Delete>
cnoremap <M-b> <S-Left>
cnoremap <M-f> <S-Right>
cnoremap <M-d> <S-Right><Delete>
cnoremap <Esc>b <S-Left>
cnoremap <Esc>f <S-Right>
cnoremap <Esc>d <S-Right><Delete>
cnoremap <C-g> <C-c>

" Open files shortcuts
noremap <leader>ee :e <C-R>=expand("%:h") . "/" <CR>
noremap <leader>ew :new %:p:h<CR>

"
" Abbreviations
"
cnoreabbrev W! w!
cnoreabbrev W w
cnoreabbrev Q! q!
cnoreabbrev Qall! qall!
cnoreabbrev Wq wq
cnoreabbrev Wa wa
cnoreabbrev wQ wq
cnoreabbrev WQ wq
cnoreabbrev Wqa wqa
cnoreabbrev W w
cnoreabbrev Q q
cnoreabbrev Qa qall
cnoreabbrev Qall qall

"
" Plugin settings
"

" BufExplorer settings
let g:bufExplorerShowRelativePath=1  " Show relative paths.

" Grepper settings
runtime plugin/grepper.vim    " initialize g:grepper with default values
let g:grepper.tools = ['git', 'rg', 'grep'] " these tools are more than enough
let g:grepper.highlight = 1   " always highlight found matches
let g:grepper.searchreg = 1   " query is added to the last search register and search
let g:grepper.dir = 'repo,filecwd' " search first the repo root

" Grepper tools settings
let g:grepper.rg.grepprg .= ' --smart-case'   " Always use smart-case when using rg

" Grepper operator mappings
nmap gs  <plug>(GrepperOperator)
xmap gs  <plug>(GrepperOperator)

" Grepper command mappings
nnoremap <leader>gg :Grepper<cr>
nnoremap <leader>gf  :Grepper -tool rg -cword -noprompt<cr>
nnoremap <leader>gb  :Grepper -tool rg -cword -noprompt -buffers<cr>
nnoremap <leader>gs  :Grepper -stop<cr>

" Grepper command mappings depending from filetype
augroup GrepperFileTypeMappings
    autocmd!
    autocmd FileType python nnoremap <buffer> <localleader>gt :Grepper -cword -noprompt -tool rg -grepprg rg -H --no-heading --vimgrep --smart-case --type py<cr>
augroup END

" Grepper colors
highlight GrepperPrompt ctermfg=160 guifg=#ff0000 cterm=NONE
highlight qfFileName ctermfg=216 ctermbg=NONE cterm=NONE guifg=#ffaf87 guibg=NONE gui=NONE
highlight QuickFixLine cterm=underline ctermfg=NONE ctermbg=NONE cterm=NONE guifg=NONE guibg=NONE gui=NONE

" Gutentags settings
let g:gutentags_enabled = 1                 " Enable gutentags...
let g:gutentags_generate_on_missing = 0     " ...but don't write a tags file if not already there
let g:gutentags_generate_on_new = 0         " ...unless you save the file.
let g:gutentags_file_list_command = { 'markers': { '.git': 'git ls-files', '.hg': 'hg files' } }
" Set Gutentags to index just python files when a python project is detected
" (detection is performed by Gutentags itself; read `:help gutentags_project_info`
" for more info). Also don't index 'import' occurences of any tags.
let g:gutentags_ctags_executable_python = 'ctags --languages=python --python-kinds=-i'

let g:gutentags_define_advanced_commands = 1    " Enable advanced command (e.g. GutentagsToggleEnabled)
nmap <leader>ct :GutentagsToggleEnabled<CR>

" Yankstack settings
let g:yankstack_map_keys = 0
nmap <leader>p <Plug>yankstack_substitute_older_paste
nmap <leader>P <Plug>yankstack_substitute_newer_paste

" Rainbow settings
let g:rainbow_active = 1
hi MatchParen ctermbg=0

" Python Enhanced Syntax Highlighting
let g:python_version_2 = 0
let g:python_highlight_all = 1

" ALE settings
let g:ale_linters_explicit = 1                       " Only run linters explicitly enabled
let g:ale_linters = { 'python': ['pylint'], 'sh': ['shellcheck'] }         " Enabled linters
let g:ale_lint_on_text_changed = 'never'             " Never run linters automatically
let g:ale_lint_on_save = 1                           " Run linters on file save
let g:ale_open_list = 'on_save'                      " Open loclist on save only

let g:ale_fixers = {'python': ['black']}             " Enabled fixers
let g:ale_fix_on_save = 1                            " Run fixers on file save
let g:ale_set_highlights = 0                         " Disable highlighting (signs are enough)

" Automatically close loclist when buffer is closed
augroup CloseLoclistWindowGroup
    autocmd!
    autocmd QuitPre * if empty(&buftype) | lclose | endif
augroup END

function! LinterStatus() abort
    let l:counts = ale#statusline#Count(bufnr(''))

    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_non_errors = l:counts.total - l:all_errors

    return l:counts.total == 0 ? 'OK' : printf(
    \   '%dW %dE',
    \   all_non_errors,
    \   all_errors
    \)
endfunction

" Indent-guides settings
let g:indent_guides_enable_on_vim_startup = 0
let g:indent_guides_guide_size = 1
let g:indent_guides_exclude_filetypes = ['help', 'terminal']

" Vim-test settings
let test#strategy = 'dispatch'
nmap <silent> <leader>tn :TestNearest<CR>
nmap <silent> <leader>tf :TestFile<CR>
nmap <silent> <leader>tl :TestLast<CR>
nmap <silent> <leader>tv :TestVisit<CR>

" Session settings
set sessionoptions=buffers,curdir,folds
let g:session_autoload='no'

" DiffChanges settings
nnoremap <silent> <Leader>df :DiffChanges<CR>
nnoremap <silent> <Leader>dt :DiffChangesDiffToggle<CR>
nnoremap <silent> <Leader>dp :DiffChangesPatchToggle<CR>

"
" Set the status line
"

set statusline=%<                                      " truncate line here if it is too long
set statusline+=%n                                     " buffer number
set statusline+=\ %f                                   " file name (relative)
set statusline+=\ %m                                   " modified flag
set statusline+=\ %h                                   " help buffer flag
set statusline+=%r                                     " read-only buffer flag
set statusline+=\ %{FugitiveStatusline()}              " show git information
set statusline+=%=                                     " separation point between left and right
set statusline+=\[%b,\0\x%B\]                          " char-code under cursor (decimal, hex)
set statusline+=\ \[%{&ff}\]                           " file-format
set statusline+=\ %l,%c%V\ %P                          " line, column, virtual col, percentage
set statusline+=\ %{grepper#statusline()}              " show grepper activity (if any)
set statusline+=\ %{gutentags#statusline(\"[\",\"]\")} " show gutentags activity (if any)
set statusline+=\ %{LinterStatus()}                    " show ALE information

"
" GUI specific settings
"
if has("gui_running")
    "set go-=T
    "set go-=L
    "set go-=r
    "set go-=m
    "set go-=b
endif

"
" Platform specific settings
"
if s:uname == "Darwin"  " Mac specific stuff
elseif s:uname == "linux" " Linux specific stuff
else " Unkwon platform
endif

